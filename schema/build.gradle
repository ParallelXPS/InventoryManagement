buildscript {
  apply from: "$rootDir/gradle/dependencies.gradle"

  ext {
    changeLogFile = "$projectDir/src/main/db/changelog-master.yaml"
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath libraries.h2
    classpath libraries.liquibase
    classpath libraries.mysql
    classpath libraries.postgres
  }
}

plugins {
  id 'java'

  id 'com.github.ben-manes.versions' version '0.15.0'
  id 'nu.studer.jooq' version '2.0.7'
  id 'org.liquibase.gradle' version '1.2.4'
}

repositories {
  jcenter()
}

dependencies {
  compile libraries.jooq
  compile libraries.jsr303
  jooqRuntime libraries.h2
}

jar {
  baseName = 'inventorySchema'
  version =  '0.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
  options.compilerArgs << '-parameters'
}

apply from: "$projectDir/gradle/jooq.gradle"

liquibase {
  activities {
    h2 {
      changeLogFile changeLogFile
      url "jdbc:h2:file:$buildDir/inventory_management"
      username 'sa'
      password ''
      contexts project.hasProperty('contexts') ? project.ext.contexts : '!test'
    }
    mysql {
      changeLogFile changeLogFile
      url mysqlUrl
      username mysqlUsername
      password mysqlPassword
      contexts project.hasProperty('contexts') ? project.ext.contexts : '!test'
    }
    postgres {
      changeLogFile changeLogFile
      url pgUrl
      username pgUsername
      password pgPassword
      contexts project.hasProperty('contexts') ? project.ext.contexts : '!test'
    }
  }
  runList = project.hasProperty('runList') ? project.ext.runList : 'h2'
}

generateSampleJooqSchemaSource.dependsOn update
