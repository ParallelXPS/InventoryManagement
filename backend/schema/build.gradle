buildscript {
  ext {
    changeLogFile = "$projectDir/src/main/resources/db/changelog/db.changelog-master.yaml"
  }
  dependencies {
    classpath libraries.h2
    classpath libraries.liquibase
    classpath libraries.mysql
    classpath libraries.postgres
  }
}

plugins {
  id 'nu.studer.jooq' version '2.0.11'
  id 'org.liquibase.gradle' version '1.2.4'
}

dependencies {
  compile libraries.jooq
  compile libraries.jsr303
  jooqRuntime libraries.h2
}

jar {
  baseName = 'inventorySchema'
}

apply from: "$projectDir/gradle/jooq.gradle"

liquibase {
  activities {
    h2 {
      changeLogFile changeLogFile
      url "jdbc:h2:file:$buildDir/inventory_management"
      username 'sa'
      password ''
      contexts project.hasProperty('contexts') ? project.ext.contexts : '!test'
    }
    mysql {
      changeLogFile changeLogFile
      url mysqlUrl
      username mysqlUsername
      password mysqlPassword
      contexts project.hasProperty('contexts') ? project.ext.contexts : '!test'
    }
    postgres {
      changeLogFile changeLogFile
      url pgUrl
      username pgUsername
      password pgPassword
      contexts project.hasProperty('contexts') ? project.ext.contexts : '!test'
    }
  }
  runList = project.hasProperty('runList') ? project.ext.runList : 'h2'
}

generateSampleJooqSchemaSource.dependsOn update

tasks.generateSampleJooqSchemaSource.with {
  javaExecSpec = { JavaExecSpec s ->
    s.jvmArgs '--add-modules', 'java.xml.bind'
  }
}
