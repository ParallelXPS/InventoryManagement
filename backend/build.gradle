plugins {
  id 'com.github.ben-manes.versions' version '0.17.0'
  id 'net.ltgt.apt' version '0.15'
  id 'net.ltgt.errorprone' version '0.0.13'
}

subprojects {
  apply from: "$rootDir/gradle/dependencies.gradle"

  repositories {
    jcenter()
  }

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'net.ltgt.apt'
  apply plugin: 'net.ltgt.errorprone'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  compileJava {
    options.compilerArgs << '-deprecation'
    options.compilerArgs << '-parameters'
    options.compilerArgs << '-Xep:NullAway:ERROR'
    options.compilerArgs << '-XepOpt:NullAway:AnnotatedPackages=com.parallelxps.graphql'
  }

  dependencies {
    apt libraries.nullaway
    compileOnly libraries.jsr305
  }

  jacoco {
    toolVersion = '0.8.1'
  }

  tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
  }

  version = '0.1.0'
}
